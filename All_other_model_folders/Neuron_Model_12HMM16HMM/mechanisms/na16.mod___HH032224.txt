TITLE na3
: Na current 
: modified from Jeff Magee. M.Migliore may97
: added sh to account for higher threshold M.Migliore, Apr.2002

NEURON {
	SUFFIX ###REMOVE###na16
	USEION na READ ena WRITE ina
	RANGE  gbar, ar2, thegna,ina_ina
	:GLOBAL vhalfs,sh,tha,qa,Ra,Rb,thi1,thi2,qd,qg,mmin,hmin,q10,Rg,qq,Rd,tq,thinf,qinf,vhalfs,a0s,zetas,gms,smax,vvh,vvs
	RANGE vhalfs,sh,tha,qa,Ra,Rb,thi1,thi2,qd,qg,mmin,hmin,q10,Rg,qq,Rd,tq,thinf,qinf,vhalfs,a0s,zetas,gms,smax,vvh,vvs
}

PARAMETER { 					:##TF030124 Params updated to match na16HH.mod in IC_Fitter in line with Hu2009(doi:10.1038/nn.2359) vals
	sh   = 8	(mV)
	gbar = 0.01   	(mho/cm2) 	: #0.010	#0.1
								
	tha  =  -59		(mV)		: v 1/2 for act #-35 #-45 #-40 #-30 #-50	
	qa   = 4.5		(mV)		: act slope (4.5)		
	Ra   = 0.4		(/ms)		: open (v) #0.4	#0.124 #0.05 #0.1 #0.2	
	Rb   = 0.4		(/ms)		: close (v) #0.124 #0.05 #0.1 #0.2 #0.001 #0.01		

	thi1  = -80		(mV)		: v 1/2 for inact #-45 #-65	#-55 #-50
	thi2  = -80 	(mV)		: v 1/2 for inact #-45 #-65 #-55 #-50
	qd   = 5.4		(mV)	    : inact tau slope
	qg   = 5.4      (mV)
	mmin=0.02	
	hmin=0.01			
	q10=2
	Rg   = 0.01 	(/ms)		: inact recov (v) #0.01 #.1 #0.001 #0.1 #0.05 #0.03 	
	Rd   = 0.03 	(/ms)		: inact (v) #0.03 #.3 #0.001 #0.1 #0.05 #0.03
	qq   = 10       (mV)
	tq   = -55      (mV)

	thinf  = -80 	(mV)		: inact inf slope	
	qinf  = 5.4		(mV)		: inact inf slope 

        vhalfs=-60	(mV)		: slow inact. #-60 #-50 #-70 #-55
        a0s=0.0003	(ms)		: a0s=b0s
        zetas=12	(1)
        gms=0.2		(1)
        smax=10		(ms)
        vvh=-58		(mV) 
        vvs=2		(mV)
        ar2=1		(1)			: 1=no inact., 0=max inact.
	ena			(mV)	
	Ena = 55	(mV)            : must be explicitly def. in hoc
	celsius
	v 		(mV)
}


UNITS {
	(mA) = (milliamp)
	(mV) = (millivolt)
	(pS) = (picosiemens)
	(um) = (micron)
} 

ASSIGNED {
	ina 		(mA/cm2)
	ina_ina     (mA/cm2)  : to monitor
	thegna		(mho/cm2)
	minf 		
	hinf 		
	mtau (ms)	
	htau (ms) 	
	sinf (ms)	
	taus (ms)
}
 

STATE { m h s}

BREAKPOINT {
        SOLVE states METHOD cnexp
        thegna = gbar*m*m*m*h*s
	ina = thegna * (v - Ena)
	ina_ina = thegna * (v -Ena) :to monitor
} 

INITIAL {
	trates(v,ar2,sh)
	m=minf  
	h=hinf
	s=sinf
}


FUNCTION alpv(v) {
         alpv = 1/(1+exp((v-vvh-sh)/vvs))
}
        
FUNCTION alps(v) {  
  alps = exp(1.e-3*zetas*(v-vhalfs-sh)*9.648e4/(8.315*(273.16+celsius)))
}

FUNCTION bets(v) {
  bets = exp(1.e-3*zetas*gms*(v-vhalfs-sh)*9.648e4/(8.315*(273.16+celsius)))
}

LOCAL mexp, hexp, sexp

DERIVATIVE states {   
        trates(v,ar2,sh)      
        m' = (minf-m)/mtau
        h' = (hinf-h)/htau
        s' = (sinf - s)/taus
}

PROCEDURE trates(vm,a2,sh2) {  
        LOCAL  a, b, c, qt
        qt=q10^((celsius-24)/10)
	a = trap0(vm,tha+sh2,Ra,qa)
	b = trap0(-vm,-tha-sh2,Rb,qa)
	mtau = 1/(a+b)/qt
        if (mtau<mmin) {
		mtau=mmin
		}
	minf = a/(a+b)

	a = trap0(vm,thi1,Rd,qd) : +sh2 raus
	b = trap0(-vm,-thi2,Rg,qg) : - sh2 raus
	htau =  1/(a+b)/qt
        if (htau<hmin) {
		htau=hmin
		}
	hinf = 1/(1+exp((vm-thinf)/qinf)): -sh2 raus
	c=alpv(vm)
        sinf = c+a2*(1-c)
        taus = bets(vm)/(a0s*(1+alps(vm)))
        if (taus<smax) {
		taus=smax
		}
}

FUNCTION trap0(v,th,a,q) {
	if (fabs(v-th) > 1e-6) {
	        trap0 = a * (v - th) / (1 - exp(-(v - th)/q))
	} else {
	        trap0 = a * q
 	}
}	